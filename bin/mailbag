#!/usr/bin/perl

package App::mailbag;

use strict;
use warnings;
use lib 'lib';
use Mail::IMAPClient;
use Mail::IMAPClient::BodyStructure;
use Encode qw(encode decode);
use Config::Tiny;
use MIME::QuotedPrint 'decode_qp';
use MIME::Base64 'decode_base64';
use Term::ShellUI;
use IO::Prompt 'prompt';
use String::Format 'stringf';
use Date::Parse 'str2time';
use Time::Piece;
use open ':locale';
use Data::Dumper;
use Term::Size;

my $width = Term::Size::chars;

my $config_file = "$ENV{HOME}/.mailbagrc";
my $config;
if ( -e $config_file ) {
    $config = Config::Tiny->read( $config_file, 'utf8' );
    if ( !$config ) {
        die "Can't read configuration: " . Config::Tiny->errstr . "\n";
    }
}
my ( $username, $server, $password ) =
  @{ $config->{_} }{qw(username server password)};

if ( not exists $config->{_}->{starttls} and not exists $config->{_}->{ssl} ) {
    $config->{_}->{starttls} = 1;
}

$username = prompt('Username: ') if !$username;
$server   = prompt('Server: ')   if !$server;
$password = prompt( 'Password: ', -e => '*' ) if !$password;

die "Can't connect unless server is supplied.\n"   if !$server;
die "Can't connect unless username is supplied.\n" if !$username;
die "Can't connect unless password is supplied.\n" if !$password;

## TODO Globals

my ( $imap, @uids, $cache, $term );

$imap = Mail::IMAPClient->new(
    Server   => $server,
    User     => $username,
    Password => $password,
    Uid      => 1,
    $config->{_}->{ssl}      ? ( Ssl      => 1 ) : (),
    $config->{_}->{starttls} ? ( Starttls => 1 ) : (),
);
die "Can't connect to imap server: $@\n" if !$imap;

$term = Term::ShellUI->new(
    prompt   => '> ',
    commands => {
        list   => { proc => \&list },
        select => {
            maxargs => 1,
            minargs => 1,
            proc    => \&select_folder,
            args    => sub { complete_folders(@_) }
        },
        folders => { maxargs => 0, proc => \&display_folders },
        quit    => {
            method => sub { shift->exit_requested(1); },
        },
        '' => {
            proc => sub {
                ## TODO Number shortcut for view number?
                print "default handler\n";
            }
        },
        view    => { proc  => \&display_msg },
        q       => { alias => 'quit' },
        create  => { proc  => \&create_folder, minargs => 1, maxargs => 1 },
        rename  => { proc  => \&rename_folder, minargs => 2, maxargs => 2 },
        copy    => { proc  => \&copy_msg, minargs => 2, maxargs => 2 },
        delete  => { proc  => \&delete, minargs => 1, maxargs => 1 },
        expunge => { proc  => \&expunge, minargs => 0, maxargs => 0 },
        sync    => { proc  => \&sync, minargs => 0, maxargs => 0 },
    }
);

select_folder('INBOX');
$term->run;

sub sync {
    ## TODO checked untagged responses
    delete $cache->{ cache_prefix() };
}

sub expunge {
    $imap->expunge( $imap->Folder ) or die "Could not expunge: $@\n";
}

sub rename_folder {
    my ( $oldname, $newname ) = @_;
    $imap->rename( $oldname, $newname )
      or die "Could not rename: $@\n";
    return;
}

sub delete {
    my ($what) = @_;
    if ( $imap->exists($what) ) {
        $imap->delete($what)
          or die "Could not delete $what: $@\n";
    }
    else {
        my @uids = get_uids($what);
        if (@uids) {
            $imap->delete_message( \@uids )
              or die "Could not delete messages: $@\n";
            update_flags(@uids);
        }
    }
    return;
}

sub create_folder {
    my $new_folder = shift;
    $imap->create($new_folder)
      or die "Could not create $new_folder: $@\n";
    return;
}

sub copy_msg {
    my ( $range, $folder ) = @_;
    my @uids = get_uids($range);
    $imap->exists($folder)
      or $imap->create($folder) && $imap->subscribe($folder);
    $imap->copy( $folder, \@uids )
      or die "Could not copy: $@\n";
    return;
}

sub get_uids {
    my $range = shift || '1-16';
    if ( $range !~ /^\d+-\d+$/ && $range !~ /^\d+$/ ) {
        die "Must be either a range or a number\n";
    }
    my ( $from, $to ) = split( /-/, $range, 2 );
    if ( !$to ) {
        return ( $from, $uids[ $from - 1 ] );
    }
    if ( $to > @uids ) {
        $to = @uids;
    }
    return ( $from, @uids[ $from - 1 .. $to - 1 ] );
}

sub get_uid {
    my $num = shift;
    $uids[ $num - 1 ];
}

sub cache_get {
    my $uid = shift;
    ## TODO Cache cache_prefix?
    $cache->{ cache_prefix() }->{$uid};
}

sub envelope {
    cache_get(shift)->{ENVELOPE};
}

sub flags {
    cache_get(shift)->{FLAGS};
}

sub list {
    my @terms = @_;
    my @search;
    for my $term (@terms) {
        if ( $term =~ /^(\d)+$/ ) {
            push @search, 'UID', $uids[ $1 - 1 ];

        }
        elsif ( $term =~ /^(\d+)-(\d+)$/ ) {
            push @search, 'UID', $imap->Range( @uids[ $1 - 1 .. $2 - 1 ] );
        }
        else {
            push @search, $term;
        }
    }

    if ( !@search ) {
        push @search, 'ALL';
    }
    @uids = $imap->sort( 'DATE', 'UTF-8', @search );

    fetch_uids(@uids);

    my $i = 1;
    for my $uid (@uids) {
        print _format_index_line( $uid, $i ), "\n";
        $i++;
    }
}

sub complete_folders {
    my ( $self, $cmp ) = @_;
    return [ grep { index( $_, $cmp->{str} ) == 0 } $imap->subscribed ];
}

sub display_folders {
    for my $folder ( $imap->folders ) {
        print $folder, "\n";
    }
    return;
}

sub _format_index_line {

    my ( $uid, $num ) = @_;
    my $env = envelope($uid);
    my %flags = map { $_ => 1 } split( ' ', flags($uid) );

    $env = Mail::IMAPClient::BodyStructure::Envelope->parse_string($env);

    my %header =
      map { $_ => decode( 'MIME-Header', $env->{$_} ) } qw(subject date);

    # TODO How to encode the string?
    my $from = $env->{from}->[0];
    ## TODO should 'NIL' be undef? That seems against the spec
    $from =
        $from->personalname ne 'NIL'
      ? $from->personalname
      : $from->mailboxname . '@' . $from->hostname;
    $header{from} = decode( 'MIME-HEADER', $from );
    $header{subject} = $header{subject} eq 'NIL' ? '' : $header{subject};
    my %formats =
      map { lc( substr( $_, 0, 1 ) ) => $header{$_} } keys %header;
    $formats{d} = sub {
        my $format = $_[0] || '%F %H:%M';
        Time::Piece->new( str2time( $header{date} ) )->strftime($format);
    };
    $formats{i} = sub { $num };
    $formats{F} = sub {
        my %flags = map { $_ => 1 } split( ' ', flags($uid) );
        my $ret = '';
        $ret .= !$flags{'\\Seen'}   ? 'N' : ' ';
        $ret .= $flags{'\\Deleted'} ? 'D' : ' ';
        $ret .= $flags{'\\Flagged'} ? '!' : ' ';
        return $ret;
    };
    return
      substr( stringf( "%-5i %F %-20.20d %-20.20f %-s", %formats ), 0, $width );
}

sub select_folder {
    my $folder = shift;
    $imap->select($folder)
      or die "Could not select: $@\n";
    return;
}

sub display_msg {
    my $num       = shift;
    my $uid       = get_uid($num);
    my $part      = $imap->get_bodystructure($uid);
    my $text_part = find_plain_text_part($part);
    my $body      = decode_mail( $uid, $text_part );
    $body =~ s/\r\n/\n/smxg;
    print $body;
}

sub find_plain_text_part {
    my ($part) = @_;
    if ( lc( $part->bodytype ) eq 'multipart' ) {
        my @parts = $part->bodystructure();
        for my $part (@parts) {
            if ( get_type($part) eq 'text/plain' ) {
                return $part;
            }
        }
    }
    elsif ( get_type($part) eq 'text/plain' ) {
        return $part;
    }
    return;
}

sub decode_mail {
    my ( $uid, $part ) = @_;
    my $body     = $imap->bodypart_string( $uid, $part->id );
    my $encoding = $part->bodyenc;
    my %decoder  = (
        'quoted-printable' => \&decode_qp,
        'base64'           => \&decode_base64,
        '8bit'             => sub { $_[0] },
        '7bit'             => sub { $_[0] },
    );
    if ($encoding) {
        $encoding = lc $encoding;
        if ( exists $decoder{$encoding} ) {
            $body = $decoder{$encoding}->($body);
        }
        else {
            boom("Can't decode mail: Unknown encoding $encoding.\n");
        }
    }
    my $charset = $part->bodyparms->{charset};
    if ($charset) {
        $body = decode( $charset, $body );
    }
    return $body;
}

sub get_type {
    my ($part) = @_;
    return lc( $part->bodytype . '/' . $part->bodysubtype );
}

sub fetch_uids {
    my (@uids) = @_;
    my $prefix = cache_prefix();
    my %keys = map { $_ => 1 } keys %{ $cache->{$prefix} };
    my @missing_uids;
    for my $uid (@uids) {
        if ( not exists $keys{$uid} ) {
            push @missing_uids, $uid;
        }
    }
    if (@missing_uids) {
        my $msgs = $imap->fetch_hash( $imap->Range(@missing_uids),
            'ENVELOPE', 'BODYSTRUCTURE', 'FLAGS' );
        for my $uid ( keys %$msgs ) {
            $cache->{$prefix}->{$uid} = $msgs->{$uid};
        }
    }
    return;
}

sub update_flags {
    my @uids = @_;
    my $msgs = $imap->fetch_hash( $imap->Range(@uids), 'FLAGS' );
    for my $uid ( keys %$msgs ) {
        $cache->{ cache_prefix() }->{$uid}->{FLAGS} = $msgs->{$uid}->{FLAGS};
    }
    return;

}

sub cache_prefix {
    my ( $user, $server, $uidvalidity ) =
      ( $imap->User, $imap->Server, $imap->uidvalidity( $imap->Folder ) );
    return "$user\@$server/$uidvalidity/";
}

1;
